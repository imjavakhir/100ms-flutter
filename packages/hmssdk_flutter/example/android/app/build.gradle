import groovy.json.JsonSlurper

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
def sdkVersions = new JsonSlurper().parse file("../../../lib/assets/sdk-versions.json")

if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    compileSdkVersion 36
    ndkVersion "27.2.12479018"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    configurations.all {
        resolutionStrategy {
            // Force all android-sdk dependencies to use the same version
            force "live.100ms:android-sdk:${sdkVersions['android']}"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "live.hms.flutter"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 532
        versionName "1.6.2"

        ndk {
            // Only support 64-bit architectures for 16KB page size support
            // Excludes: armeabi-v7a (32-bit ARM), x86 (32-bit x86)
            abiFilters 'arm64-v8a', 'x86_64'
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
            enableV1Signing false
            enableV2Signing true
        }
    }
    buildTypes {
        release {
            shrinkResources true

            minifyEnabled true

            debuggable false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
    lint {
        checkDependencies true
        abortOnError false
    }
    android.buildTypes.release.ndk.debugSymbolLevel "FULL"

    packaging {
        jniLibs {
            // Enable proper packaging for 16KB page size support
            // useLegacyPackaging=false ensures uncompressed, page-aligned .so files
            useLegacyPackaging = false
        }
        resources {
            // Exclude 32-bit architectures to ensure 16KB compatibility
            excludes += ['lib/armeabi-v7a/**', 'lib/x86/**']
        }
    }

    namespace 'live.hms.flutter'
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation("live.100ms:hms-noise-cancellation-android:${sdkVersions['android']}") {
        // Exclude transitive android-sdk dependency since we already get it from hmssdk_flutter plugin
        exclude group: 'live.100ms', module: 'android-sdk'
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
